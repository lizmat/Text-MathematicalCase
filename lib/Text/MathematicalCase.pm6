
# Most of the information to create the mapper was obtained from
# https://en.wikipedia.org/wiki/Mathematical_Alphanumeric_Symbols

my constant %raw =
  ":serif" => (
      "A" .. "Z",               # A .. Z
      "a" .. "z",               # a .. z
      "0" .. "9",               # 0 .. 9
  ),
  ":serif:bold" => (
      "\x1D400" .. "\x1D419",   # ğ€ .. ğ™
      "\x1D41A" .. "\x1D433",   # ğš .. ğ³
      "\x1D7CE" .. "\x1D7d7",   # ğŸ .. ğŸ—
  ),
  ":serif:italic" => (
      "\x1D434" .. "\x1D44D",   # ğ´ .. ğ‘
      ( "\x1D44E" .. "\x1D454", "\x0210E", "\x1D456" .. "\x1D467"
      ),                        # ğ‘ .. ğ‘§
      "\x1D7F6" .. "\x1D7FF",   # ğŸ¶ .. ğŸ¿
  ),
  ":serif:bold:italic" => (
      "\x1D468" .. "\x1D481",   # ğ‘¨ .. ğ’
      "\x1D482" .. "\x1D49B",   # ğ’‚ .. ğ’›
      "\x1D7CE" .. "\x1D7d7",   # ğŸ .. ğŸ—
  ),
  ":sans-serif" => (
      "\x1D5A0" .. "\x1D5B9",   # ğ–  .. ğ–¹
      "\x1D5BA" .. "\x1D5D3",   # ğ–º .. ğ—“
      "\x1D7E2" .. "\x1D7EB",   # ğŸ¢ .. ğŸ«
  ),
  ":sans-serif:bold" => (
      "\x1D5D4" .. "\x1D5ED",   # ğ—” .. ğ—­
      "\x1D5EE" .. "\x1D607",   # ğ—® .. ğ˜‡
      "\x1D7EC" .. "\x1D7F5",   # ğŸ¬ .. ğŸµ
  ),
  ":sans-serif:italic" => (
      "\x1D608" .. "\x1D621",   # ğ˜ˆ .. ğ˜¡
      "\x1D622" .. "\x1D63B",   # ğ˜¢ .. ğ˜»
      "\x1D7E2" .. "\x1D7EB",   # ğŸ¢ .. ğŸ«
  ),
  ":sans-serif:bold:italic" => (
      "\x1D63C" .. "\x1D655",   # ğ˜¼ .. ğ™•
      "\x1D656" .. "\x1D66F",   # ğ™– .. ğ™¯
      "\x1D7EC" .. "\x1D7F5",   # ğŸ¬ .. ğŸµ
  ),
  ":script" => (
      ( "\x1D49C", "\x0212C", "\x1D49E", "\x1D49F", "\x02130", "\x02131",
        "\x1D4A2", "\x0210B", "\x02110", "\x1D4A5", "\x1D4A6", "\x02112",
        "\x02133", "\x1D4A9", "\x1D4AA", "\x02118", "\x1D4AC", "\x0211B",
        "\x1D4AE" .. "\x1D4B5"
      ),                        # ğ’œ .. ğ’µ
      ( "\x1D4B6" .. "\x1D4B9", "\x0212F", "\x1D4BB", "\x0210A",
        "\x1D4BD" .. "\x1D4C3", "\x02134", "\x1D4C5" .. "\x1D4CF"
      ),                        # ğ’¶ .. ğ“
      "\x1D7F6" .. "\x1D7FF",   # ğŸ¶ .. ğŸ¿
  ),
  ":script:bold" => (
      "\x1D4D0" .. "\x1D4E9",   # ğ“ .. ğ“©
      "\x1D4EA" .. "\x1D503",   # ğ“ª .. ğ”ƒ
      "\x1D7CE" .. "\x1D7d7",   # ğŸ .. ğŸ—
  ),
  ":fraktur" => (
      ( "\x1D504", "\x1D505", "\x0212D", "\x1D507" .. "\x1D50A", "\x0210C",
        "\x02111", "\x1D50D" .. "\x1D514", "\x0211C", "\x1D516" .. "\x1D51C",
        "\x02128"
      ),                        # ğ”„ .. â„¨
      "\x1D51E" .. "\x1D537",   # ğ” .. ğ”·
      "\x1D7F6" .. "\x1D7FF",   # ğŸ¶ .. ğŸ¿
  ),
  ":fraktur:bold" => (
      "\x1D56C" .. "\x1D585",   # ğ•¬ .. ğ–…
      "\x1D586" .. "\x1D59F",   # ğ–† .. ğ–Ÿ
      "\x1D7CE" .. "\x1D7d7",   # ğŸ .. ğŸ—
  ),
  ":monospace" => (
      "\x1D670" .. "\x1D689",   # ğ™° .. ğš‰
      "\x1D68A" .. "\x1D6A3",   # ğšŠ .. ğš£
      "\x1D7F6" .. "\x1D7FF",   # ğŸ¶ .. ğŸ¿
  ),
  ":double-struck" => (
      ( "\x1D538", "\x1D539", "\x02102", "\x1D53B" .. "\x1D53E", "\x0210D",
        "\x1D540" .. "\x1D544", "\x02115", "\x1D546", "\x02119", "\x0211A",
        "\x0211D", "\x1D54A" .. "\x1D550", "\x02124",
      ),                        # ğ”¸ .. â„¤
      "\x1D552" .. "\x1D56B",   # ğ•’ .. ğ•«
      "\x1D7D8" .. "\x1D7E1",   # ğŸ˜ .. ğŸ¡
  ),
;

my constant @adverbs    := %raw.keys.sort.List;
my constant $multiplier := @adverbs.end;

my constant %mapper = do {
    my %mapper;
    my @lc-source;
    my @lc-target;
    my @uc-source;
    my @uc-target;

    for @adverbs -> $adverbs {
        my @info := %raw{$adverbs};

        @lc-source.push(@info[0]);  # upper ->
        @lc-target.push(@info[1]);  #          lower
        @uc-source.push(@info[1]);  # lower ->
        @uc-target.push(@info[0]);  #          upper

        %mapper{$adverbs} := Pair.new(
          @adverbs.map( -> $type {
              |(%raw{$type}.flat) if $type ne $adverbs;
          } ).List.join,
          (|(@info.flat) xx $multiplier).join
        )
    }

    # temporary store in the mapper, so we can create constants later
    %mapper<lc> := Pair.new(@lc-source.flat.join, @lc-target.flat.join);
    %mapper<uc> := Pair.new(@uc-source.flat.join, @uc-target.flat.join);

    %mapper.Map
}

my constant $lc-mapper = %mapper<lc>;
my constant $uc-mapper = %mapper<uc>;

module Text::MathematicalCase:ver<0.0.1>:auth<cpan:ELIZABETH> {
    my sub mc(Cool:D \string,
      :$serif, :$sans-serif, :$script, :$fraktur, :$monospace, :$double-struck,
      :$italic, :$bold
    --> Str:D) is export(:all) is hidden-from-backtrace {

        my str $adverbs;
        $adverbs ~= ':serif'         if $serif;
        $adverbs ~= ':sans-serif'    if $sans-serif;
        $adverbs ~= ':script'        if $script;
        $adverbs ~= ':fraktur'       if $fraktur;
        $adverbs ~= ':monospace'     if $monospace;
        $adverbs ~= ':double-struck' if $double-struck;
        $adverbs ~= ':bold'          if $bold;
        $adverbs ~= ':italic'        if $italic;

        if %mapper{$adverbs} -> $mapper {
            string.trans($mapper)
        }
        else {
            Failure.new(X::Adverb.new(
              :what<mc>,
              :source(try { string.VAR.name } // string.WHAT.raku),
              :nogo($adverbs.substr(1).split(":"))
            ))
        }
    }

    my sub lc(Cool:D \string --> Str:D) is export(:all) {
        string.lc.trans($lc-mapper)
    }
    my sub uc(Cool:D \string --> Str:D) is export(:all) {
        string.uc.trans($uc-mapper)
    }
    my sub adverbs(--> List:D) is export(:all) {
        @adverbs
    }
}

sub EXPORT(*@args, *%_) {
    if @args {
        my $imports := Map.new( |(EXPORT::all::{ @args.map: '&' ~ * }:p) );
        if $imports != @args {
            die "Text::MathematicalCase doesn't know how to export: "
              ~ @args.grep( { !$imports{$_} } ).join(', ')
        }
        $imports
    }
    else {
        Map.new('&mc' => EXPORT::all::<&mc>)
    }
}

=begin pod

=head1 NAME

Text::MathematicalCase - convert to/from mathematical case

=head1 SYNOPSIS

=begin code :lang<raku>

  use Text::MathematicalCase;        # just mc
  say mc "Hello World" :serif:bold;  # ğ‡ğğ¥ğ¥ğ¨ ğ–ğ¨ğ«ğ¥ğ

  use Text::MathematicalCase :all;   # mc lc uc adverbs
  say uc "ğ‡ğğ¥ğ¥ğ¨ ğ–ğ¨ğ«ğ¥ğ";              # ğ‡ğ„ğ‹ğ‹ğ ğ–ğğ‘ğ‹ğƒ

=end code

=head1 DESCRIPTION

Text::MathematicalCase is module that exports an C<mc> subroutine that
implements converting to/from "mathematical case".  Just like you can
have UPPERCASE or lowercase, you can also have ğ¦ğšğ­ğ¡ğğ¦ğšğ­ğ¢ğœğšğ¥ ğœğšğ¬ğ.

"Mathematical case" is basically text expressed in the alphanumeric
symbols of the L<Mathematical Alphanumeric Symbols|https://en.wikipedia.org/wiki/Mathematical_Alphanumeric_Symbols>
unicode block.  In it, several styles are supported:

- serif: serif, ğ¬ğğ«ğ¢ğŸ ğ›ğ¨ğ¥ğ, ğ‘ ğ‘’ğ‘Ÿğ‘–ğ‘“ ğ‘–ğ‘¡ğ‘ğ‘™ğ‘–ğ‘, ğ’”ğ’†ğ’“ğ’Šğ’‡ ğ’ƒğ’ğ’ğ’… ğ’Šğ’•ğ’‚ğ’ğ’Šğ’„

- sans-serif: ğ—Œğ–ºğ—‡ğ—Œ-ğ—Œğ–¾ğ—‹ğ—‚ğ–¿, ğ˜€ğ—®ğ—»ğ˜€-ğ˜€ğ—²ğ—¿ğ—¶ğ—³ ğ—¯ğ—¼ğ—¹ğ—±, ğ˜´ğ˜¢ğ˜¯ğ˜´-ğ˜´ğ˜¦ğ˜³ğ˜ªğ˜§ ğ˜ªğ˜µğ˜¢ğ˜­ğ˜ªğ˜¤, ğ™¨ğ™–ğ™£ğ™¨-ğ™¨ğ™šğ™§ğ™ğ™› ğ™—ğ™¤ğ™¡ğ™™ ğ™ğ™©ğ™–ğ™¡ğ™ğ™˜

- script: ğ“ˆğ’¸ğ“‡ğ’¾ğ“…ğ“‰, ğ“¼ğ“¬ğ“»ğ“²ğ“¹ğ“½ ğ“«ğ“¸ğ“µğ“­

- fraktur: ğ”£ğ”¯ğ”ğ”¨ğ”±ğ”²ğ”¯, ğ–‹ğ–—ğ–†ğ–ğ–™ğ–šğ–— ğ–‡ğ–”ğ–‘ğ–‰

- monospace: ğš–ğš˜ğš—ğš˜ğšœğš™ğšŠğšŒğš

- double-struck: ğ••ğ• ğ•¦ğ•“ğ•ğ•–-ğ•¤ğ•¥ğ•£ğ•¦ğ•”ğ•œ

It aptionally also exports an C<lc> and/or a C<uc> subroutine (that
perform the same function as the standard C<lc> and C<uc> subroutines,
but are aware of mathematical case characters).  And it optionally exports
an C<adverbs> subroutine that lists all the possible combinations of
adverbs that can be passed on to the C<mc> subroutine.

This distribution also installs a C<mc> script for easy access to the
mathematical case functionality.

=head1 SUBROUTINES

=head2 mc

  say mc "Hello World" :serif:bold;  # ğ‡ğğ¥ğ¥ğ¨ ğ–ğ¨ğ«ğ¥ğ

Convert a string to mathematical case with the given adverbs.

=head2 lc

  use Text::MathematicalCase <lc>;
  say lc "ğ‡ğğ¥ğ¥ğ¨ ğ–ğ¨ğ«ğ¥ğ";  # ğ¡ğğ¥ğ¥ğ¨ ğ°ğ¨ğ«ğ¥ğ

Convert a string to lowercase taking mathematical case into account as well.

=head2 uc

  use Text::MathematicalCase <uc>;
  say uc "ğ‡ğğ¥ğ¥ğ¨ ğ–ğ¨ğ«ğ¥ğ";  # ğ‡ğ„ğ‹ğ‹ğ ğ–ğğ‘ğ‹ğƒ

Convert a string to uppercase taking mathematical case into account as well.

=head2 adverbs

  use Text::MathematicalCase <adverbs>;
  .say for adverbs;
  # :double-struck
  # :fraktur
  # :fraktur:bold
  # :monospace
  # :sans-serif
  # :sans-serif:bold
  # :sans-serif:bold:italic
  # :sans-serif:italic
  # :script
  # :script:bold
  # :serif
  # :serif:bold
  # :serif:bold:italic
  # :serif:italic

=head1 SCRIPTS

=head2 mc

  $ mc "Hello World" --double-struck
  â„ğ•–ğ•ğ•ğ•  ğ•ğ• ğ•£ğ•ğ••

  $ mc --double-struck < file-with-text
  â„‚ğ• ğ•Ÿğ•¥ğ•–ğ•Ÿğ•¥ ğ• ğ•— ğ•—ğ•šğ•ğ•–-ğ•¨ğ•šğ•¥ğ•™-ğ•¥ğ•–ğ•©ğ•¥

The C<mc> script either takes a string, or reads from C<STDIN> and performs
the mathematical case transformation as indicated by its named arguments.

=head1 AUTHOR

Elizabeth Mattijsen <liz@wenzperl.nl>

Source can be located at: https://github.com/lizmat/Text-MathematicalCase .
Comments and Pull Requests are welcome.

=head1 COPYRIGHT AND LICENSE

Copyright 2020 Elizabeth Mattijsen

This library is free software; you can redistribute it and/or modify it under the Artistic License 2.0.

=end pod

# vim: expandtab shiftwidth=4
