
# Most of the information to create the mapper was obtained from
# https://en.wikipedia.org/wiki/Mathematical_Alphanumeric_Symbols

my constant %raw =
  ":serif" => (                               # UNCOVERABLE
      "A" .. "Z",               # A .. Z
      "a" .. "z",               # a .. z
      "0" .. "9",               # 0 .. 9
  ),
  ":serif:bold" => (                          # UNCOVERABLE
      "\x1D400" .. "\x1D419",   # ð€ .. ð™
      "\x1D41A" .. "\x1D433",   # ðš .. ð³
      "\x1D7CE" .. "\x1D7d7",   # ðŸŽ .. ðŸ—
  ),
  ":serif:italic" => (                        # UNCOVERABLE
      "\x1D434" .. "\x1D44D",   # ð´ .. ð‘
      ( "\x1D44E" .. "\x1D454", "\x0210E", "\x1D456" .. "\x1D467"
      ),                        # ð‘Ž .. ð‘§
      "\x1D7F6" .. "\x1D7FF",   # ðŸ¶ .. ðŸ¿
  ),
  ":serif:bold:italic" => (                   # UNCOVERABLE
      "\x1D468" .. "\x1D481",   # ð‘¨ .. ð’
      "\x1D482" .. "\x1D49B",   # ð’‚ .. ð’›
      "\x1D7CE" .. "\x1D7d7",   # ðŸŽ .. ðŸ—
  ),
  ":sans-serif" => (                          # UNCOVERABLE
      "\x1D5A0" .. "\x1D5B9",   # ð–  .. ð–¹
      "\x1D5BA" .. "\x1D5D3",   # ð–º .. ð—“
      "\x1D7E2" .. "\x1D7EB",   # ðŸ¢ .. ðŸ«
  ),
  ":sans-serif:bold" => (                     # UNCOVERABLE
      "\x1D5D4" .. "\x1D5ED",   # ð—” .. ð—­
      "\x1D5EE" .. "\x1D607",   # ð—® .. ð˜‡
      "\x1D7EC" .. "\x1D7F5",   # ðŸ¬ .. ðŸµ
  ),
  ":sans-serif:italic" => (                   # UNCOVERABLE
      "\x1D608" .. "\x1D621",   # ð˜ˆ .. ð˜¡
      "\x1D622" .. "\x1D63B",   # ð˜¢ .. ð˜»
      "\x1D7E2" .. "\x1D7EB",   # ðŸ¢ .. ðŸ«
  ),
  ":sans-serif:bold:italic" => (              # UNCOVERABLE
      "\x1D63C" .. "\x1D655",   # ð˜¼ .. ð™•
      "\x1D656" .. "\x1D66F",   # ð™– .. ð™¯
      "\x1D7EC" .. "\x1D7F5",   # ðŸ¬ .. ðŸµ
  ),
  ":script" => (                              # UNCOVERABLE
      ( "\x1D49C", "\x0212C", "\x1D49E", "\x1D49F", "\x02130", "\x02131",
        "\x1D4A2", "\x0210B", "\x02110", "\x1D4A5", "\x1D4A6", "\x02112",
        "\x02133", "\x1D4A9", "\x1D4AA", "\x02118", "\x1D4AC", "\x0211B",
        "\x1D4AE" .. "\x1D4B5"
      ),                        # ð’œ .. ð’µ
      ( "\x1D4B6" .. "\x1D4B9", "\x0212F", "\x1D4BB", "\x0210A",
        "\x1D4BD" .. "\x1D4C3", "\x02134", "\x1D4C5" .. "\x1D4CF"
      ),                        # ð’¶ .. ð“
      "\x1D7F6" .. "\x1D7FF",   # ðŸ¶ .. ðŸ¿
  ),
  ":script:bold" => (                         # UNCOVERABLE
      "\x1D4D0" .. "\x1D4E9",   # ð“ .. ð“©
      "\x1D4EA" .. "\x1D503",   # ð“ª .. ð”ƒ
      "\x1D7CE" .. "\x1D7d7",   # ðŸŽ .. ðŸ—
  ),
  ":fraktur" => (                             # UNCOVERABLE
      ( "\x1D504", "\x1D505", "\x0212D", "\x1D507" .. "\x1D50A", "\x0210C",
        "\x02111", "\x1D50D" .. "\x1D514", "\x0211C", "\x1D516" .. "\x1D51C",
        "\x02128"
      ),                        # ð”„ .. â„¨
      "\x1D51E" .. "\x1D537",   # ð”ž .. ð”·
      "\x1D7F6" .. "\x1D7FF",   # ðŸ¶ .. ðŸ¿
  ),
  ":fraktur:bold" => (                        # UNCOVERABLE
      "\x1D56C" .. "\x1D585",   # ð•¬ .. ð–…
      "\x1D586" .. "\x1D59F",   # ð–† .. ð–Ÿ
      "\x1D7CE" .. "\x1D7d7",   # ðŸŽ .. ðŸ—
  ),
  ":monospace" => (                           # UNCOVERABLE
      "\x1D670" .. "\x1D689",   # ð™° .. ðš‰
      "\x1D68A" .. "\x1D6A3",   # ðšŠ .. ðš£
      "\x1D7F6" .. "\x1D7FF",   # ðŸ¶ .. ðŸ¿
  ),
  ":double-struck" => (                       # UNCOVERABLE
      ( "\x1D538", "\x1D539", "\x02102", "\x1D53B" .. "\x1D53E", "\x0210D",
        "\x1D540" .. "\x1D544", "\x02115", "\x1D546", "\x02119", "\x0211A",
        "\x0211D", "\x1D54A" .. "\x1D550", "\x02124",
      ),                        # ð”¸ .. â„¤
      "\x1D552" .. "\x1D56B",   # ð•’ .. ð•«
      "\x1D7D8" .. "\x1D7E1",   # ðŸ˜ .. ðŸ¡
  ),
;

my constant @adverbs    := %raw.keys.sort.List;
my constant $multiplier := @adverbs.end;

my constant %mapper = do {
    my %mapper;
    my @lc-source;
    my @lc-target;
    my @uc-source;
    my @uc-target;

    for @adverbs -> $adverbs {
        my @info := %raw{$adverbs};

        @lc-source.push(@info[0]);  # upper ->
        @lc-target.push(@info[1]);  #          lower
        @uc-source.push(@info[1]);  # lower ->
        @uc-target.push(@info[0]);  #          upper

        %mapper{$adverbs} := Pair.new(
          @adverbs.map( -> $type {
              |(%raw{$type}.flat) if $type ne $adverbs;
          } ).List.join.NFD,
          (|(@info.flat) xx $multiplier).join.NFD
        )
    }

    # temporary store in the mapper, so we can create constants later
    %mapper<lc> := Pair.new(@lc-source.flat.join.NFD, @lc-target.flat.join.NFD);
    %mapper<uc> := Pair.new(@uc-source.flat.join.NFD, @uc-target.flat.join.NFD);

    %mapper.Map
}

my constant $lc-mapper = %mapper<lc>;
my constant $uc-mapper = %mapper<uc>;

my sub trans(Str:D \string, Pair:D \mapper --> Str:D) {
    my @source     := string.NFD;
    my @haystack   := mapper.key;
    my @translated := mapper.value;

    my uint32 @result;
    for @source -> int $needle {
        with @haystack.first($needle, :k) {
            @result.push(@translated[$_]);
        }
        else {
            @result.push($needle);
        }
    }

    Uni.new(@result).Str;
}

my sub mc(Cool:D \string,
  :$serif, :$sans-serif, :$script, :$fraktur, :$monospace, :$double-struck,
  :$italic, :$bold
--> Str:D) is export(:all) is hidden-from-backtrace {

    my str $adverbs;
    $adverbs  = ':serif'         if $serif;
    $adverbs ~= ':sans-serif'    if $sans-serif;
    $adverbs ~= ':script'        if $script;
    $adverbs ~= ':fraktur'       if $fraktur;
    $adverbs ~= ':monospace'     if $monospace;
    $adverbs ~= ':double-struck' if $double-struck;

    $adverbs ||= ':serif';  # allow for just :italic and :bold
    $adverbs ~= ':bold'   if $bold;
    $adverbs ~= ':italic' if $italic;

    if %mapper{$adverbs} -> $mapper {
        trans(string, $mapper)
    }
    else {
        Failure.new(X::Adverb.new(
          :what<mc>,
          :source(try { string.VAR.name } // string.WHAT.raku),
          :nogo($adverbs.substr(1).split(":"))
        ))
    }
}

my sub lc(Cool:D \string --> Str:D) is export(:all) {
    trans(string.lc, $lc-mapper)
}
my sub uc(Cool:D \string --> Str:D) is export(:all) {
    trans(string.uc, $uc-mapper)
}
my sub adverbs(--> List:D) is export(:all) {
    @adverbs
}

sub EXPORT(*@args, *%_) {
    if @args {
        my $imports := Map.new( |(EXPORT::all::{ @args.map: '&' ~ * }:p) );
        if $imports != @args {
            die "Text::MathematicalCase doesn't know how to export: "
              ~ @args.grep( { !$imports{$_} } ).join(', ')
        }
        $imports
    }
    else {
        Map.new('&mc' => EXPORT::all::<&mc>)
    }
}

# vim: expandtab shiftwidth=4
